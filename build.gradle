plugins {
  id 'org.springframework.boot' version '2.6.3'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'org.asciidoctor.convert' version '1.5.8'
  id 'java'
  id 'com.adarshr.test-logger' version '3.1.0'
}

group = 'com.vitechteam'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()
}

ext {
  set('snippetsDir', file("build/generated-snippets"))
  set('testcontainersVersion', "1.16.2")
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-validation'

  implementation 'org.springframework.boot:spring-boot-starter-actuator'

  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
  implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

  //  for GH secret encryption
  implementation "com.goterl:lazysodium-java:5.1.1"
  implementation "net.java.dev.jna:jna:5.10.0"

  //  code style
  implementation 'com.google.code.findbugs:jsr305:3.0.2'

  implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.1'

  compileOnly 'org.projectlombok:lombok'
  compileOnly 'org.mapstruct:mapstruct'
  annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
  annotationProcessor 'org.projectlombok:lombok'
  annotationProcessor 'org.mapstruct:mapstruct-processor'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
  testImplementation 'org.testcontainers:junit-jupiter'
  testImplementation 'org.awaitility:awaitility'
  testImplementation 'org.mapstruct:mapstruct'
  testAnnotationProcessor 'org.mapstruct:mapstruct-processor'

  implementation group: 'org.kohsuke', name: 'github-api', version: '1.301'

  implementation 'org.springdoc:springdoc-openapi-webmvc-core:1.6.6'
  implementation 'org.springdoc:springdoc-openapi-ui:1.6.6'

//  implementation 'io.jsonwebtoken:jjwt:0.9.1'
//  implementation 'jakarta.xml.bind:jakarta.xml.bind-api'
}

dependencyManagement {
  imports {
    mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
  }

  dependencies {
    dependencySet(group: "org.mapstruct", version: "1.4.2.Final") {
      entry "mapstruct"
      entry "mapstruct-processor"
    }
    dependency(group: "org.awaitility", name: "awaitility", version: "4.1.1")
  }
}

compileJava {
  options.compilerArgs
          << "-Amapstruct.unmappedTargetPolicy=ERROR"
          << "-Amapstruct.defaultComponentModel=spring"
}

compileTestJava {
  options.compilerArgs
          << "-Amapstruct.unmappedTargetPolicy=ERROR"
          << "-Amapstruct.defaultComponentModel=spring"
}

tasks.named('test') {
  outputs.dir snippetsDir
  useJUnitPlatform {
    includeTags "small", "medium"
  }
}

tasks.register('largeTest', Test) {
  outputs.dir snippetsDir
  useJUnitPlatform {
    includeTags "large"
  }
}

tasks.named('asciidoctor') {
  inputs.dir snippetsDir
  dependsOn test
}
